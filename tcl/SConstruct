#*****************************************************************************
# Copyright 2013, Qualcomm Innovation Center, Inc.
#
#    All rights reserved.
#    This file is licensed under the 3-clause BSD license in the NOTICE.txt
#    file for this project. A copy of the 3-clause BSD license is found at:
#
#        http://opensource.org/licenses/BSD-3-Clause.
#
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the license is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the license for the specific language governing permissions and
#    limitations under the license.
#*****************************************************************************

# usage:
# scons VARIANT=debug TARG=linux WS=off 
#    optional additional parameters:
#        AJ_TCL_ROOT=path/to/ajtcl
#        <name of target to build>
# sample usage:
# scons VARIANT=debug TARG=linux WS=off AJ_TCL_ROOT=../../../ajtcl

ajtcl_root_default = '../../../ajtcl'

vars = Variables()
import os

vars.Add(PathVariable('AJ_TCL_ROOT', 'The path to alljoyn ThinClient software', ajtcl_root_default, PathVariable.PathIsDir))
vars.Add(EnumVariable('WS', 'Whitespace Policy Checker', 'check', allowed_values=('check', 'detail', 'fix', 'off')))

env = Environment(variables = vars)

Export('env')

# based on ajtcl/SConscript:
if env['WS'] != 'off' and not env.GetOption('clean'):
    if not env.has_key('AJ_TCL_ROOT'):
       print "AJ_TCL_ROOT not set"
       if not GetOption('help'):
           Exit()

    # Set the location of the uncrustify config file
    env['uncrustify_cfg'] = File(env['AJ_TCL_ROOT'] + '/ajuncrustify.cfg').abspath

    import sys
    bin_dir = env['AJ_TCL_ROOT'] + '/../build_core/tools/bin'
    sys.path.append(bin_dir)
    import whitespace

    def wsbuild(target, source, env):
        print "Evaluating whitespace compliance..."
        print "Note: enter 'scons -h' to see whitespace (WS) options"
        return whitespace.main([env['WS'],env['uncrustify_cfg']])

    env.Command('#/ws', Dir('tmp'), wsbuild)

env = SConscript(env['AJ_TCL_ROOT'] + '/SConscript')

# we create a cloned ajtcl environment, so we don't taint ajtcl build with our defines and flags
iskoot_env = env.Clone()
Export('iskoot_env')
iskoot_env.SConscript('SConscript')
